'20230625
Sub ファイルの存在チェック()
    ' Shellオブジェクトを作成
    Set wsh = CreateObject("WScript.Shell")
    ' FileSystemObjectを作成 --- (*1)
    Set fso = CreateObject("Scripting.FileSystemObject")
    
    ' デスクトップにある「test.txt」のパスを得る --- (*2)
    DesktopPath = wsh.SpecialFolders("Desktop")
    FilePath = DesktopPath & "\test.txt"
    ' パスの存在を調べる --- (*3
    If fso.FileExists(FilePath) Then
        MsgBox "test.txtがあります"
    Else
        MsgBox "test.txtというファイルはありません。"
    End If
End Sub

Sub fstest()
    Dim fso As Object
    
    ' Shellオブジェクトを作成
    Set wsh = CreateObject("WScript.Shell")
    ' FileSystemObjectを作成
    Set fso = CreateObject("Scripting.FileSystemObject")
    DesktopPath = wsh.SpecialFolders("Desktop")
    FilePath = DesktopPath & "\test.txt"
    Set ts = fso.CreateTextFile(FilePath, True)
    ts.WriteLine ("This is a test.")
    ts.Close
End Sub

Sub テキスト書き込み()
    ' ブックと同じフォルダの「test.txt」
    Textfile = ThisWorkbook.Path & "\test.txt"
    ' ファイルを書き込みモードで開く --- (*1)
    Open Textfile For Output As #1
    ' 格言を書き込む --- (*2)
    Print #1, "人生はパンのためだけにあらず"
    ' ファイルを閉じる --- (*3)
    Close #1
End Sub

Sub テキスト一行読み込み()
    ' ブックと同じフォルダの「test.txt」
    Textfile = ThisWorkbook.Path & "\test.txt"
    ' ファイルを読み込みモードで開く --- (*1)
    Open Textfile For Input As #1
    ' 一行読む --- (*2)
    Line Input #1, Text
    ' ファイルを閉じる -- (*3)
    Close #1
    ' メッセージボックスに表示
    MsgBox Text
End Sub


Sub テキスト全部読み込み()
    ' ブックと同じフォルダの「test2.txt」
    Textfile = ThisWorkbook.Path & "\test2.txt"
    
    ' 複数行のテキストを書き込む
    Open Textfile For Output As #1
    Print #1, "おはよう"
    Print #1, "こんにちは"
    Print #1, "こんばんは"
    Close #1
    
    ' ファイルを読み込みモードで開く --- (*1)
    Open Textfile For Input As #1
    ' ファイルの末尾まで繰り返し読む --- (*2)
    Do While Not EOF(1)
        Line Input #1, Text
        Debug.Print Text
    Loop
    ' ファイルを閉じる --- (*3)
    Close #1
End Sub


Sub UTF8でテキスト書き込み()
    ' ブックと同じフォルダの「test-utf8.txt」
    Textfile = ThisWorkbook.Path & "\test-utf8.txt"
    CrLf = Chr(13) & Chr(10)
    
    ' Streamオブジェクトを生成 --- (*1)
    Set stream = CreateObject("ADODB.Stream")
    
    ' 文字エンコーディングを指定して開く --- (*2)
    stream.Charset = "UTF-8"
    stream.Open
    
    ' 書き込む --- (*3)
    stream.WriteText _
        "「ゴールは遠いなぁ」と" & CrLf & _
        "がっかりするのも道のりです" & CrLf
    
    ' ファイルを保存して閉じる --- (*4)
    stream.SaveToFile Textfile, 2
    stream.Close
End Sub


Sub UTF8でテキスト読み込み()
    ' ブックと同じフォルダの「test-utf8.txt」
    Textfile = ThisWorkbook.Path & "\test-utf8.txt"
    
    ' Streamオブジェクトを生成 --- (*1)
    Set stream = CreateObject("ADODB.Stream")
    
    ' 文字エンコーディングを指定して開く --- (*2)
    stream.Charset = "UTF-8"
    stream.Open
    
    ' ファイルから読み込み閉じる -- (*3)
    stream.LoadFromFile Textfile
    Text = stream.ReadText
    stream.Close
    
    MsgBox Text
End Sub

Sub BOMなしのUTF8でテキストを書き込む()
    Filename = ThisWorkbook.Path & "\test-utf8n.txt"
    Text = "正しい人は7回倒れても，また立ち上がる。"
    WriteToFileUTF8N Filename, Text
End Sub

' BOMなしのUTF-8でファイルにテキストを書き込む
Sub WriteToFileUTF8N(Filename, Text)
    Set stream = CreateObject("ADODB.Stream")
    With stream
        .Type = 2 ' テキストモードを指定
        .Charset = "UTF-8"
        .Open
        .WriteText Text ' テキストを書き込む
        .Position = 0 ' カーソルをファイル先頭に
        .Type = 1 ' バイナリモードに変更
        .Position = 3 ' BOM(3バイト)を飛ばす
        buf = .Read() ' 内容を読み込む
        .Position = 0 ' カーソルを先頭に
        .Write buf ' BOMなしのテキストを書き込み
        .SetEOS
        .SaveToFile Filename, 2
        .Close
    End With
End Sub

        
